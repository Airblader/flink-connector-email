plugins {
    id "java"
    id "com.diffplug.spotless" version "5.14.2"
}

group "com.tngtech"
version "0.1-SNAPSHOT"

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"
}

spotless {
    java {
        importOrder()
        removeUnusedImports()

        prettier([
                "prettier"            : "2.0.5",
                "prettier-plugin-java": "0.8.0"
        ]).config([
                "parser"  : "java",
                "tabWidth": 4
        ])
    }
}

configurations {
    extraLibs
}

jar {
    enabled = true
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task copyToDocker(type: Copy) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from jar
    into "${rootDir}/testing/images/client/jars"
}

jar.finalizedBy copyToDocker

clean.doFirst {
    delete "${rootDir}/testing/images/client/jars"
}

ext {
    flink = "1.13.1"
    lombok = "1.18.20"
    greenmail = "2.0.0-alpha-1"
}

dependencies {
    compileOnly "org.apache.flink:flink-table-common:${flink}"
    compileOnly "org.apache.flink:flink-table-api-java-bridge_2.12:${flink}"

    extraLibs "com.squareup.okhttp3:okhttp:4.9.1"
    extraLibs "com.sun.mail:jakarta.mail:2.0.1"

    // Lombok
    compileOnly "org.projectlombok:lombok:${lombok}"
    annotationProcessor "org.projectlombok:lombok:${lombok}"
    testCompileOnly "org.projectlombok:lombok:${lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombok}"

    // Testing

    testImplementation "org.apache.flink:flink-table-planner-blink_2.12:${flink}"
    testImplementation "org.apache.flink:flink-streaming-scala_2.12:${flink}"
    testImplementation "org.apache.flink:flink-test-utils_2.12:${flink}"
    testImplementation "com.icegreen:greenmail-junit4:${greenmail}"
    testImplementation "org.awaitility:awaitility-groovy:4.1.0"
    testImplementation "com.tngtech.junit.dataprovider:junit4-dataprovider:2.8"
    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation 'com.tngtech.archunit:archunit-junit4:0.20.1'

    //

    configurations.compile.extendsFrom(configurations.extraLibs)
}
